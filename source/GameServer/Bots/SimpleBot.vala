using Gee;

class SimpleBot : Bot
{
    private Engine.RandomClass rnd = new Engine.RandomClass();

    protected override void do_turn_decision()
    {
        if (round_state.can_tsumo())
        {
            do_tsumo();
        }
        else if (round_state.can_void_hand())
        {
            do_void_hand();
        }
        else if (round_state.can_late_kan())
        {
            ArrayList<Tile> tiles = TileRules.get_late_kan_tiles(round_state.self.hand, round_state.self.calls);
            assert(tiles.size > 0);

            do_late_kan(tiles[0]);
        }
        else if (round_state.can_closed_kan())
        {
            ArrayList<ArrayList<Tile>> groups = round_state.self.get_closed_kan_groups();
            assert(groups.size > 0);

            do_closed_kan(groups[0][0].tile_type);
        }
        else
        {
            Tile tile = get_discard_tile();
            do_discard(tile);
        }
    }

    protected override void do_call_decision(RoundStatePlayer discarding_player, Tile tile)
    {
        if (round_state.can_ron(round_state.self))
        {
            call_ron();
            return;
        }
        else if (round_state.can_pon(round_state.self) && count(tile) == 2)
        {
            if (tile.is_dragon_tile() || tile.is_wind(round_state.self.wind) || tile.is_wind(round_state.round_wind))
            {
                call_pon();
                return;
            }
        }
        /*else if (round_state.can_chii(tile, round_state.self, round_state.discard_player))
        {
            action_delay();

            ArrayList<ArrayList<Tile>> groups = TileRules.get_chii_groups(round_state.self.hand, tile);
            ArrayList<Tile> tiles = groups[0];

            call_chii(tiles[0], tiles[1]);
        }*/

        call_nothing();
    }

    private Tile get_discard_tile()
    {
        ArrayList<Tile> tiles = round_state.self.get_discard_tiles();
        assert(tiles.size > 0);

        ArrayList<Tile> copy = new ArrayList<Tile>();
        foreach (Tile tile in tiles)
        {
            copy.add_all(tiles);
            copy.remove(tile);

            if (TileRules.in_tenpai(copy, null))
                return tile;

            copy.clear();
        }

        ArrayList<Tile> backup = new ArrayList<Tile>();
        backup.add_all(tiles);

        for (int i = 0; i < tiles.size; i++)
        {
            Tile tile = tiles[i];
            if (count(tile) >= 3)
                tiles.remove_at(i--);
        }

        if (tiles.size == 0)
            return RandomTile(backup);

        foreach (Tile tile in tiles)
        {
            if (tile.is_wind_tile())
            {
                if (!tile.is_wind(round_state.self.wind) && !tile.is_wind(round_state.round_wind))
                    return tile;
                else if (count(tile) <= 1)
                    return tile;
            }
            if (tile.is_dragon_tile())
            {
                if(count(tile) <= 1)
                    return tile;
            }
        }

        backup.clear();
        backup.add_all(tiles);

        for (int i = 0; i < tiles.size; i++)
        {
            Tile tile = tiles[i];
            if (tile.is_dragon_tile() || tile.is_wind(round_state.self.wind) || tile.is_wind(round_state.round_wind))
                tiles.remove_at(i--);
        }

        if (tiles.size == 0)
            return RandomTile(backup);

        backup.clear();
        backup.add_all(tiles);

        for (int i = 0; i < tiles.size; i++)
        {
            Tile tile = tiles[i];
            if (has_neighbours(tile))
                tiles.remove_at(i--);
        }

        if (tiles.size == 0)
            return RandomTile(backup);

        backup.clear();
        backup.add_all(tiles);

        for (int i = 0; i < tiles.size; i++)
        {
            Tile tile = tiles[i];
            if (count(tile) >= 2)
                tiles.remove_at(i--);
        }

        if (tiles.size == 0)
            return RandomTile(backup);

        backup.clear();
        backup.add_all(tiles);

        for (int i = 0; i < tiles.size; i++)
        {
            Tile tile = tiles[i];
            if (has_second_neighbours(tile))
                tiles.remove_at(i--);
        }

        if (tiles.size == 0)
            return RandomTile(backup);

        backup.clear();
        backup.add_all(tiles);

        for (int i = 0; i < tiles.size; i++)
        {
            Tile tile = tiles[i];
            if (!tile.is_terminal_tile())
                tiles.remove_at(i--);
        }

        if (tiles.size == 0)
            return RandomTile(backup);

        return RandomTile(tiles);
    }

    private Tile RandomTile(ArrayList<Tile> tiles)
    {
        return tiles[rnd.int_range(0, tiles.size)];
    }

    private int count(Tile tile)
    {
        int count = 0;
        foreach (Tile t in round_state.self.hand)
            if (t.tile_type == tile.tile_type)
                count++;
        return count;
    }

    public override string name { get { return "SimpleBot"; } }
}
